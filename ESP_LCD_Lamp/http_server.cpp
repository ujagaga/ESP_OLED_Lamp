/* 
 *  Author: Rada Berar
 *  email: ujagaga@gmail.com
 *  
 *  HTTP server which generates the web browser pages. 
 */

#include <ESP8266WebServer.h>
#include <pgmspace.h>
#include "wifi_connection.h"
#include "config.h"
#include "pinctrl.h"
#include "ESP_LCD_Lamp.h"

// --- HTML templates ---
static const char HTML_BEGIN[] PROGMEM = R"(
<!DOCTYPE HTML>
<html>
  <head>
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=0">
    <title>Secret Santa Clock</title>
    <style>
      body { background-color: white; font-family: Arial, Helvetica, Sans-Serif; color: #000000; }
      .contain{width: 100%;}
      .center_div{margin:0 auto; max-width: 400px;position:relative;}
    </style>
  </head>
  <body>
)";

static const char HTML_END[] PROGMEM = "</body></html>";

static const char INDEX_HTML_0[] PROGMEM = R"(
<style>
  .btn_b{border:0;border-radius:0.3rem;color:#fff;line-height:4rem;font-size:3rem;margin:1%;height:4rem;width:4rem;background-color:#1fa3ec;flex:1;}
  .btn_cfg{border:0;border-radius:0.3rem;color:#fff;line-height:1.4rem;font-size:0.8rem;margin:1ch;height:2rem;width:10rem;background-color:#ff3300;}      
  .row{display: flex;justify-content: space-between;align-items: center;}   
  .lightBtnOff, .lightBtnOn{width: 140px; height: 140px;position: relative;	margin-left: auto;margin-right: auto;	margin-top: 70px;	z-index: 100;	border-radius: 50%;}
  .lightBtnOff::before, .lightBtnOff::after, .lightBtnOff span, .lightBtnOn::before, .lightBtnOn::after, .lightBtnOn span{display: block;	content: "";position: absolute;}
  .lightBtnOff::before, .lightBtnOn::before{width: 120px;	height: 120px;background: black;	border-radius: 50%;	top: 10px;left: 10px;}
  .lightBtnOff::before{	border: 2px solid #ACABA4;}
  .lightBtnOn::before{border: 2px solid white;box-shadow: 0 0 20px white;}
  .lightBtnOff::after, .lightBtnOn::after{width: 70px;height: 70px;	top: 31px;left: 31px;	border-radius: 50%;}
  .lightBtnOff::after{	background: black;	border: 6px solid #8a8a5c;}
  .lightBtnOn::after{	background: black;	border: 6px solid #66ff66;}
  .lightBtnOff span, .lightBtnOn span{	width: 8px;	height: 20px;		border: 6px solid black;	top: 22px;	left: 62px;	z-index: 4;}
  .lightBtnOff span{	background: #8a8a5c;}
  .lightBtnOn span{	background: #66ff66;}   
</style>
<div class="contain">
  <div class="center_div">
  <div class='row'>
    <div class="lightBtnOn" id="tgl" onclick="toggleLight();">
    <span></span>
    </div>
  </div>
  </div>
  <hr>
  <a href="https://github.com/ujagaga/ESP_OLED_Lamp" target="_blank" rel="noopener noreferrer">Source code</a>
)";

const char INDEX_HTML_1[] PROGMEM = R"(  
  <br>
  <button class="btn_cfg" type="button" onclick="location.href='/selectap';">Configure WiFi</button>
  <br/>
</div>
<script>
  var current = 0;
  var cn=new WebSocket('ws://'+location.hostname+':81/');
  cn.onopen=function(){
    cn.send('{"STATUS":""}');    
  };
  cn.onmessage=function(e){
    var data=JSON.parse(e.data);

    if(data.hasOwnProperty('CURRENT')){
      if(data.CURRENT == 0){
        document.getElementById('tgl').classList.add('lightBtnOff');
        document.getElementById('tgl').classList.remove('lightBtnOn');
      }else{
        document.getElementById('tgl').classList.remove('lightBtnOff');
        document.getElementById('tgl').classList.add('lightBtnOn');
      }	
    } 		  
  };
  function toggleLight() {    
    cn.send('{"TOGGLE": "1"}');
  }
</script>
)";

static const char APLIST_HTML_0[] PROGMEM = R"(
<style>
  .c{text-align: center;}
  div,input{padding:5px;font-size:1em;}
  input{width:95%;}
  body{text-align: left;}
  button{width:100%;border:0;border-radius:0.3rem;color:#fff;line-height:2.4rem;font-size:1.2rem;height:40px;background-color:#1fa3ec;}
  .q{float: right;width: 64px;text-align: right;}
  .radio{width:2em;}
  #vm{width:100%;height:50vh;overflow-y:auto;margin-bottom:1em;} 
</style>
</head><body>  
  <div class="contain">
    <div class="center_div">
)";    

static const char APLIST_HTML_1[] PROGMEM = R"(   
      <h1 id='ttl'>Networks found:</h1>
      <div id='vm'>
)";  

static const char APLIST_HTML_2[] PROGMEM = R"( 
      </div>
      <form method='get' action='wifisave'>
        <button type='button' onclick='refresh();'>Rescan</button><br/><br/>
        <input id='s' name='s' length=32 placeholder='SSID (Leave blank for AP mode)'><br>      
        <input id='p' name='p' length=32 placeholder='Password'><br>        
        <br><button type='submit'>Save</button>        
      </form>      
     </div>
  </div>
<script>
  function c(l){
    document.getElementById('s').value=l.innerText||l.textContent;
    document.getElementById('p').focus();
  }
  
  var cn=new WebSocket('ws://'+location.hostname+':81/');
  cn.onopen=function(){
    cn.send('{"APLIST":""}');
  }
  cn.onmessage=function(e){
    var data=JSON.parse(e.data);
    if(data.hasOwnProperty('APLIST')){
      rsp=data.APLIST.split('|');
      document.getElementById('vm').innerHTML='';
      for(var i=0;i<(rsp.length);i++){
        document.getElementById('vm').innerHTML+='<span>'+(i+1)+": </span><a href='#p' onclick='c(this)'>" + rsp[i] + '</a><br>';
      }
      if(!document.getElementById('vm').innerHTML.replace(/\\s/g,'').length){
        document.getElementById('ttl').innerHTML='No networks found.'
      } 
    }
  };
  function refresh(){
    document.getElementById('vm').innerHTML='Please wait...'
    cn.send('{"APLIST":""}');
  } 
</script>
)";

static const char REDIRECT_HTML[] PROGMEM = R"(
<p id="tmr"></p>
<script>
  var c=6;   
  function count(){
    var tmr=document.getElementById('tmr');   
    if(c>0){
      c--;
      tmr.innerHTML="You will be redirected to home page in "+c+" seconds.";
      setTimeout('count()',1000);
    }else{
      window.location.href="/";
    }
  }
  count();
</script> 
)";

// --- Web server object ---
ESP8266WebServer* webServer = nullptr;

// --- Page handlers ---
void showStartPage() { 
  String response = FPSTR(HTML_BEGIN);
  response += FPSTR(INDEX_HTML_0);
  response += "<p>Station IP: " + WIFIC_getStationIp() + "</p>";
  response += FPSTR(INDEX_HTML_1); 
  response += FPSTR(HTML_END);
  webServer->send(200, "text/html", response);  
}


static void showNotFound(void){
  webServer->send(404, "text/html; charset=iso-8859-1","<html><head> <title>404 Not Found</title></head><body><h1>Not Found</h1></body></html>"); 
}

static void showStatusPage(bool goToHome = false) {    
  String response = FPSTR(HTML_BEGIN);
  response += "<h1>Connection Status</h1><p>";
  response += MAIN_getStatusMsg() + "</p>";
  if(goToHome){
    response += FPSTR(REDIRECT_HTML);
  }
  response += FPSTR(HTML_END);
  webServer->send(200, "text/html", response);   
}

static void selectAP(void) {   
  String response = FPSTR(HTML_BEGIN);
  response += FPSTR(APLIST_HTML_0);  
  response += FPSTR(APLIST_HTML_1);
  response += "Please wait...";  
  response += FPSTR(APLIST_HTML_2);   
  response += FPSTR(HTML_END);
  webServer->send(200, "text/html", response);  
}

static void saveWiFi(void){
  String ssid = webServer->arg("s");
  String pass = webServer->arg("p");
  
  if((ssid.length() > 63) || (pass.length() > 63)){
      MAIN_setStatusMsg("Sorry, this module can only remember SSID and a PASSWORD up to 63 bytes long.");
      showStatusPage(true); 
      return;
  } 

  String st_ssid = WIFIC_getStSSID();
  String st_pass = WIFIC_getStPass();

  if(st_ssid.equals(ssid) && st_pass.equals(pass)){
      MAIN_setStatusMsg("All parameters are already set as requested.");
      showStatusPage(true);      
      return;
  }   

  WIFIC_setStSSID(ssid);
  WIFIC_setStPass(pass);

  String http_statusMessage;

  if(ssid.length() > 3){    
    http_statusMessage = "Saving settings and connecting to SSID: " + ssid;    
  }else{       
    http_statusMessage = "No SSID selected...";    
  }

  MAIN_setStatusMsg(http_statusMessage);
  showStatusPage();

  WIFIC_stationMode();
}

// --- Public functions ---
void HTTP_SERVER_process(void){
  webServer->handleClient(); 
}

void HTTP_SERVER_init(void){   
  if (webServer != nullptr) {
    delete webServer; // Clean up old one
  }
  webServer = new ESP8266WebServer(80);

  webServer->on("/", showStartPage);
  webServer->on("/favicon.ico", showNotFound);
  webServer->on("/selectap", selectAP);
  webServer->on("/wifisave", saveWiFi);
  webServer->onNotFound(showStartPage);
  
  webServer->begin();
}
